
//Gets all trend bars for the last number of bars 
// example call would be getTrendBars(C>Ref(C,-1), 2)
function getTrendBars(conditionArray, lastBars) {
    result = Null;
	for(bar =lastBars; bar < BarCount; bar++) {
	    condition=True;
		for(lBar=bar-lastBars; lBar < bar; lBar++) {
			if(!conditionArray[lBar]) {
				condition=False;
				break;
			}
		}
		if(condition) {
		   result[bar] = bar;
		}
	}
	return result;
}

function getUpTrend(timePeriod) {
	return getTrendBars( O < C && C > Ref(C, -1), timePeriod);
}

function getDownTrend(timePeriod) {
	return getTrendBars( O > C && C < Ref(C, -1), timePeriod);
}

function llBarSince(previousBar, currBar){
   pBars = LLVBars(L, currBar-previousBar);
   return currBar-pBars[currBar];
}

function hhBarSince(previousBar, currBar){
   pBars = HHVBars(H, currBar-previousBar);
   return currBar-pBars[currBar];
}

function setTrendLine(trendLine, i1Array, i2Array, sBar, eBar) {
    slope = ((i2Array[eBar] - i1Array[sBar])/(eBar-sBar));
	for(i=sBar ;i<=eBar ;i++ ) {
		trendLine[i] = i1Array[sBar] + (i-sBar) * slope;
	}
	return trendLine;
}

function setSR(hlValue){
   sr0 = StaticVarGet("sr-0");
   if(IsNull(sr0) || sr0 != hlValue) {
	   for(i=4; i>=1; i--){
		   prev = StaticVarGet("sr-"+(i-1));
		   if(!IsNull(prev)) {
			 StaticVarSet("sr-"+i, prev);
		   }
		}
		StaticVarSet("sr-0", hlValue);
    }
}


function isUpGannTrend(){
    sr0 = StaticVarGet("sr-0");
    sr1 = StaticVarGet("sr-1");
    sr2 = StaticVarGet("sr-2");
    sr3 = StaticVarGet("sr-3");
    sr4 = StaticVarGet("sr-4");
    isGannTrend = Null;
	if(!IsNull(sr0) && !IsNull(sr1) && !IsNull(sr2) && !IsNull(sr3)) {
	   if(sr2 < sr0 && (sr3 < sr0 || sr3 < sr1)) {
		 isGannTrend = True;
	   } else {
	    if(sr0 < sr2 && (sr0 < sr3 || sr1 < sr3)) {
			isGannTrend = False;
	    }
	   }
	}
	return isGannTrend;
}

hTrends=lTrends=Null;
uGannTrend=dGannTrend=Null;
function drawTrendLines(upTrendBars, dnTrendBars) {
	pBar=prevBar=0;
	upTrend=Null;
	trendLine = Null;
	isGannUpTrend= Null;
	for(bar =1; bar < BarCount; bar++) {
		if(upTrendBars[bar] != 0) {
		  pBar = llBarSince(prevBar, bar);
		  hBar = hhBarSince(pBar, bar);
		  if(!upTrend) { //to blend trends - this is a fix for gap up between trend changes 
			 trendLine = setTrendLine(trendLine, H, L, prevBar, pBar);
		  }
		  trendLine = setTrendLine(trendLine, L, H, pBar, hBar);
		  lTrends[pBar]=L[pBar];
		  ll = L[pBar];
		  setSR(ll);
		  upTrend=True;
		  isGannUpTrend = isUpGannTrend();
		  if(!IsNull(isGannUpTrend) && isGannUpTrend) {
			uGannTrend[bar]=True;
		  } 
		} else {
			if(dnTrendBars[bar] != 0) {
			  pBar = hhBarSince(prevBar, bar);
			  lBar = llBarSince(pBar, bar);
			  if(upTrend) {
				trendLine = setTrendLine(trendLine, L, H, prevBar, pBar);
			  }
			  trendLine = setTrendLine(trendLine, H, L, pBar, lBar);
			  hTrends[pBar]=H[pBar];
			  hh = H[pBar];
			  setSR(hh);
			  upTrend=False;
			  isGannUpTrend = isUpGannTrend();
			  if(!IsNull(isGannUpTrend) && !isGannUpTrend) {
				dGannTrend[bar]=True;
			  }
			} 
		}
		
		if(upTrendBars[bar] != 0 || dnTrendBars[bar] != 0) {
			prevBar = pBar;
		}else {
			if(upTrend) {
				hBar = hhBarSince(prevBar, bar);
				trendLine = setTrendLine(trendLine, L, H, prevBar, hBar);
			}else {
				if(!upTrend) {
					//pBar = hhBarSince(prevBar, bar);
					lBar = llBarSince(prevBar, bar);
					trendLine = setTrendLine(trendLine, H, L, prevBar, lBar);
				}
			}
		}
	}
	Plot(trendLine, "Trend Line", colorYellow, styleLine);
	//PlotShapes( IIF( hTrends, shapeDigit9 + shapePositionAbove, shapeNone ), colorGreen ); 
	//PlotShapes( IIF( lTrends, shapeDigit0, shapeNone ), colorRed );
	return trendLine;
}



SetTradeDelays(0, 0, 0, 0);
oTimeperiod = Optimize("Time Period", 4, 2, 4, 1);
timePeriod = oTimeperiod;//Param("Time Period", 3, 2, 6, 1);
upTrend = getUpTrend(timePeriod);
dnTrend = getDownTrend(timePeriod);
upTrend = ExRem(upTrend, dnTrend);
dnTrend = ExRem(dnTrend, upTrend);

Plot(Close,"Close",colorBlack,styleBarNoTicks | styleCloud, Null, Null, Null, -1 );
//PlotShapes(IIf(upTrend, Low +shapeUpArrow, shapeNone), colorGreen);
//PlotShapes(IIf(dnTrend, High + shapeDownArrow, shapeNone), colorRed); 
drawTrendLines(upTrend, dnTrend);

PlotShapes(IIf(uGannTrend, Low * shapeDigit1, shapeNone), colorGreen);
PlotShapes(IIf(dGannTrend, High * shapeDigit2, shapeNone), colorRed); 

Buy=uGannTrend;//upTrend;
Sell=dGannTrend;//dnTrend;
Short = Sell;
Cover = Buy;


ApplyStop(stopTypeLoss, 
          stopModePercent, 
          1,//Optimize( "Max. loss stop level", 1, 1, 10, 1 ), 
          True); 

/*          
ApplyStop(stopTypeProfit, 
			stopModePercent, 
			Optimize( "Trailing profit stop level", 8, 1, 30, 1 ), 
			True);*/
			
ApplyStop(stopTypeTrailing , 
			stopModePercent, 
			Optimize( "Trailing profit stop level", 6, 1, 30, 1 ), 
			True);