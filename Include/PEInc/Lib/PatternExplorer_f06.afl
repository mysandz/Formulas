/// -----------------------------------------------------------------------------------------
/// PatternExplorer.com - PatternExplorer_f06
/// Copyright © PatternExplorer.com
/// @link https://www.PatternExplorer.com
/// Email: support@PatternExplorer.com
/// -----------------------------------------------------------------------------------------

per = (LastValue(ATR(30) / C)) * 10 * Sensitivity;
x = Cum(1);
StartPoint = 1;
PS = TroughBars(IIf(PatternPriceBasis, L, C), per, 1) == 0;
xb = LastValue(ValueWhen(PS, x, StartPoint));
xa = LastValue(ValueWhen(PS, x, StartPoint + 1));
yb = LastValue(ValueWhen(PS, IIf(PatternPriceBasis, L, C), StartPoint));
Ya = LastValue(ValueWhen(PS, IIf(PatternPriceBasis, L, C), StartPoint + 1));
xab_log = log(yb / yA) / (xb - xa);
SL = exp((x - xb) * xab_log) * yb;
RocSL = ROC(SL, 1);
PR1 = PeakBars(IIf(PatternPriceBasis, H, C), per, 1) == 0;
Xd = LastValue(ValueWhen(pR1, x, StartPoint));
xc = LastValue(ValueWhen(pR1, x, StartPoint + 1));
yd = LastValue(ValueWhen(pR1, IIf(PatternPriceBasis, H, C), StartPoint));
Yc = LastValue(ValueWhen(pR1, IIf(PatternPriceBasis, H, C), StartPoint + 1));
xcd_log = log(yd / yc) / (xd - xc);
RL = exp((x - xd) * xcd_log) * yd;
RocRL = ROC(RL, 1);
Low_Value = LastValue(Ref(LLV(IIf(PatternPriceBasis, L, C), xd - xc), -(x - xd)));
x2 = LastValue(ValueWhen(IIf(PatternPriceBasis, L, C) == Low_Value &  x > xc & x < xd, x), StartPoint);
PLL = IIf(LastValue(x) - x2 > 5, exp((x - x2) * xcd_log) * Low_Value, -1e10);
Hi_Value = LastValue(Ref(HHV(IIf(PatternPriceBasis, H, C), xb - xa), -(x - xb)));
x3 = LastValue(ValueWhen(IIf(PatternPriceBasis, H, C) == Hi_Value & x > xa & x < xb, x), StartPoint);
PHL = IIf(LastValue(x) - x3 > 5, exp((x - x3) * xab_log) * Hi_Value, -1e10);
AvgRoc = abs(MA(ROC(C, 1), 100));
Threshold_parallel = 15;
Threshold_Trend = 0.001 * per;

PeCPR();

Distance = 0.5 * ATR(14);
Sup_break = Cross(SL, C) & X > XB;
Sup_PriceJustAbove = L < SL + Distance & L > SL & X > XB;
Sup_Confirmed = L <= SL & C > SL & X > XB;
Res_Break = Cross(C, RL) & X > XD;
Res_PriceJustBelow = H > RL - Distance & H<RL & X>XD;
Res_Confirmed = H >= RL & C<RL & X>XD;
PLL_Break = Cross(PLL, C);
PLL_PriceJustAbove = L < PLL + Distance & L > PLL;
PLL_Confirmed = L <= PLL & C > PLL;
PHL_Break = Cross(C, PHL);
PHL_PriceJustBelow = H > PHL - Distance & H < PHL;
PHL_Confirmed = H >= PHL & C < PHL;
SupSignals = Sup_break | Sup_PriceJustAbove | Sup_Confirmed;
ResSignals = Res_Break | Res_PriceJustBelow | Res_Confirmed;
PLLSignals = PLL_Break | PLL_PriceJustAbove | PLL_Confirmed;
PHLSignals = PHL_Break | PHL_PriceJustBelow | PHL_Confirmed;
BS = Sup_Confirmed OR Res_Break;
SS = Res_Confirmed OR Sup_break;
NS = Sup_PriceJustAbove OR Res_PriceJustBelow;
PatternDirection =
    IIf(AscT | FW | UT, 1,
         IIf(DscT | RW | DT, -1,
              IIf(ST | ET | Rectangle, 0, 0)));
PatternText =
    WriteIf(LastValue(AscT), "Ascending Triangle",
             WriteIf(LastValue(DscT), "Decending Triangle",
                      WriteIf(LastValue(ST), "Symmetrical Triangle",
                               WriteIf(LastValue(ET), "Expanding Triangle",
                                        WriteIf(LastValue(RW), "Rising Wedge",
                                                 WriteIf(LastValue(FW), "Falling Wedge",
                                                          WriteIf(LastValue(UT), "Up Channel",
                                                                   WriteIf(LastValue(DT), "Down Channel",
                                                                            WriteIf(Rectangle, "Rectangle", "Not defined")))))))));
AnZ = ya == 0 OR yb == 0 OR yc == 0 OR yd == 0;
BEGIN = Min(xa, xc);
END = RL < SL;
TRS = IIf(x >= BEGIN AND NOT END, SL, Null);
TRR = IIf(x >= BEGIN AND NOT END, RL, Null);
DRval = DisRange * LastValue(ATR(10));
PLLd = LastValue(PLL) > LastValue(C) - DRval AND LastValue(PLL) < LastValue(C) + DRval;
PHLd = LastValue(PHL) > LastValue(C) - DRval AND LastValue(PHL) < LastValue(C) + DRval;
barvisible = Status("barvisible");
firstvisiblebar = barvisible & NOT Ref(barvisible, -1);
HHvisible = LastValue(HighestSince(firstvisiblebar, High));
LLvisible = LastValue(LowestSince(firstvisiblebar, Low));
RaH = HHvisible * 1.50;
RaL = LLVisible * 0.50;
PLL_plot = IIf(x - x2 >= 0 & PLL > RaL & PLL < RaH & NOT AnZ AND PLLd, PLL, IIf(x - x2 >= 0 & RaL == 0 & PLLd & NOT AnZ, PLL, -1e10));
PHL_plot = IIf(x - x3 >= 0 & PHL > RaL & PHL < RaH & NOT AnZ AND PHLd, PHL, IIf(x - x3 >= 0 & RaL == 0 & PHLd & NOT AnZ, PHL, -1e10));
DefinedPatterns = AscT | DscT | ST | ET | RW | FW | UT | DT;